import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'java'
}

group 'com.jordansne'
version '2.0.0'

def bukkitVersion = '1.15.2-R0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots'
    }
    maven {
        url 'https://repo.codemc.org/repository/maven-public'
    }
}

dependencies {
    shadow (group: 'org.bukkit', name: 'bukkit', version: bukkitVersion)
    implementation 'org.bstats:bstats-bukkit:1.7'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.3'
}

processResources {
    filter(ReplaceTokens, tokens: [ version: version ])
}

jar {
    archiveClassifier.set 'sourceOnly'
}

shadowJar {
    minimize()
    relocate 'org.bstats', 'com.jordansne.bstats'
    archiveClassifier.set ''
}
