import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
}

group 'com.jordansne'
version '2.0.0'

def bukkitVersion = '1.15.2-R0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots'
    }
}

configurations {
    bundle
    implementation.extendsFrom(bundle)
}

dependencies {
    implementation (group: 'org.bukkit', name: 'bukkit', version: bukkitVersion)
    bundle 'org.apache.httpcomponents.client5:httpclient5:5.0'
    bundle 'com.fasterxml.jackson.core:jackson-databind:2.10.3'
}

processResources {
    filter(ReplaceTokens, tokens: [ version: version ])
}

jar {
    from {
        configurations.bundle.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
